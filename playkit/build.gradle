apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: 'version.gradle'

android {
    compileSdkVersion 31
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionName playkitVersion  // defined in version.gradle
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String","VERSION_NAME","\"${playkitVersion}\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        lintConfig file("lint.xml")
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

tasks.withType(Javadoc) {
    options.addBooleanOption('Xdoclint:none', true)
}

dependencies {

    def exoPlayerVersion = '2.15.1'
    api "com.kaltura.playkit:kexoplayer:$exoPlayerVersion"

    api 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.annotation:annotation:1.2.0'

    // Ok is (optionally) used by ExoPlayer now
    api 'com.squareup.okhttp3:okhttp:4.9.1'

    // Kotlin Config
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.github.jitpack:gradle-simple:1.0'

    def checkerframeworkVersion = '3.3.0'
    def checkerframeworkCompatVersion = '2.5.0'

    compileOnly 'org.checkerframework:checker-qual:' + checkerframeworkVersion
    compileOnly 'org.checkerframework:checker-compat-qual:' + checkerframeworkCompatVersion

    def guavaVersion = '27.1-android'

    api ('com.google.guava:guava:' + guavaVersion) {
        exclude group: 'org.checkerframework', module: 'checker-compat-qual'
    }
    // Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation "org.mockito:mockito-core:2.28.2"
    androidTestImplementation 'androidx.test:runner:1.3.0'
}

if (!ext.playkitVersion.contains('dev')) {
    apply from: './gradle-mvn-push.gradle'
} else {
    apply from: './gradle-mvn-local.gradle'
}
