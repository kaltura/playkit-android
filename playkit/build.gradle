apply plugin: 'com.android.library'
apply from: 'version.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionName playkitVersion  // defined in version.gradle
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        lintConfig file("lint.xml")
    }
}

android.libraryVariants.all { variant ->

    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        title = null // doclava does not support title
        destinationDir = new File("${project.buildDir}/javadoc/", variant.baseName)
        source = variant.javaCompile.source

        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)

        description "Generates Javadoc for $variant.name."

        exclude '**/R.java'
        options.doclet("com.google.doclava.Doclava")
        options.docletpath(new File(project.rootDir, "tools/doclava.jar"))
    }
}

dependencies {
    // Google Extras
    compile 'com.google.android.exoplayer:exoplayer:r2.1.1'
    compile 'com.google.code.gson:gson:2.7'

    // Google Ads
    compile 'com.google.ads.interactivemedia.v3:interactivemedia:3.5.2'
    compile 'com.google.android.gms:play-services-ads:10.0.1'

    // Google Cast
    compile 'com.google.android.gms:play-services-cast-framework:10.0.1'
    compile 'com.android.support:mediarouter-v7:25.0.1'

    // OkHttp
    compile 'com.squareup.okhttp3:okhttp:3.4.1'

    // Youbora Analytics
    compile files('lib/YouboraLib-5.3.0.jar')

    // Tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile "org.mockito:mockito-core:2.2.26"
    androidTestCompile 'com.android.support.test:runner:0.5'
}

